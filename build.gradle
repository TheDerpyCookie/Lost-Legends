plugins {
	id 'dev.architectury.loom' version '1.6-SNAPSHOT' apply false
	id 'architectury-plugin' version '3.4-SNAPSHOT'
}

architectury {
	minecraft = minecraft_version
}

subprojects {
	apply plugin: 'dev.architectury.loom'
	apply plugin: 'architectury-plugin'

	dependencies {
		minecraft "com.mojang:minecraft:${minecraft_version}"
		mappings loom.layered {
			it.mappings "net.fabricmc:yarn:${yarn_mappings}"
			it.mappings "dev.architectury:yarn-mappings-patch-neoforge:${yarn_mappings_patch}"
		}
	}
}

allprojects {
	apply plugin: "java"

	group = maven_group
	version = mod_version
	archivesBaseName = "${mod_id}-mc${minecraft_version}"

	processResources {
		def expandProps = [
			'modId'              : mod_id,
			'modName'            : mod_name,
			'modDescription'     : mod_description,
			'modVersion'         : mod_version,
			'modAuthor'          : mod_author,
			"minecraftVersion"   : minecraft_version,
			"minMinecraftVersion": min_minecraft_version,
			"fabricLoaderVersion": fabric_loader_version,
			"fabricApiVersion"   : fabric_api_version,
			"neoForgeVersion"    : neoforge_version
		]

		filesMatching(['fabric.mod.json', 'META-INF/neoforge.mods.toml']) {
			expand expandProps
		}

		inputs.properties(expandProps)
	}

	repositories {
		maven {
			name = "Jitpack"
			url 'https://jitpack.io/'
		}
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = 21
	}

	java {
		withSourcesJar()

		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
	}
}
